plugins {
    id 'groovy'
    id 'org.jetbrains.kotlin.jvm' version '1.1.1'
    id 'java-gradle-plugin'
}

apply from: 'gradle-mvn-push.gradle'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}
dependencies {
    compile 'com.android.tools.build:gradle:2.3.0'
    compile('com.google.apis:google-api-services-androidpublisher:v2-rev41-1.22.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    compile 'org.jetbrains.kotlin:kotlin-stdlib-jre7:1.1.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.6.2'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

gradlePlugin {
    plugins {
        play {
            id = 'com.github.triplet.play'
            implementationClass = 'de.triplet.gradle.play.PlayPublisherPlugin'
        }
    }
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.project {
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies = pom.dependencies.findAll { dep ->
                        dep.scope != 'test'
                    }
                }
            }
        }
    }
}

// Compile Kotlin then Groovy
afterEvaluate {
    compileKotlin.dependsOn = compileKotlin.taskDependencies.values - 'compileJava'
    compileGroovy.dependsOn compileKotlin
    compileGroovy.classpath += files(compileKotlin.destinationDir)
    classes.dependsOn compileGroovy
}
